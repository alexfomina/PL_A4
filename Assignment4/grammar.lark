?start: exp


?exp: exp "+" exp  -> plus
     | exp "-" exp  -> minus
     | exp "^" exp  -> power
     | exp "<=" exp -> leq
     | exp "==" exp -> eq
     | "if" exp "then" exp "else" exp -> if_expr
     | "let" NAME "=" exp "in" exp -> let
     | "letrec" NAME "=" exp "in" exp -> letrec
     | "fix" exp -> fix
     | exp ";;" exp -> prog
     | "hd" exp -> hd
     | "tl" exp -> tl
     | "#" -> nil
     | exp ":" exp -> cons
     | exp1

?exp1: exp1 "*" exp2 -> times
     | "-" exp1      -> neg
     | exp2

?exp2: exp2 exp3     -> app
     | exp3

?exp3: "\\" NAME "." exp -> lam
     | NAME              -> var
     | NUMBER            -> num
     | "(" exp ")"       -> parens



NAME: /[a-z_][a-zA-Z0-9_]*/         
%import common.NUMBER

%import common.WS
%ignore WS

%ignore /\/\/[^\n]*/  
%ignore /\/\*[\s\S]*?\*\//